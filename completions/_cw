#compdef cw
# Zsh completion for cw (Claude Code workspace switcher)

_cw() {
    local -a commands
    commands=(
        'list:Show all profiles'
        'add:Create a new profile'
        'remove:Delete a profile'
        'login:Log in with a profile'
        'set-key:Set or update API key'
        'use:Start Claude Code with a profile'
        'alias:Print shell aliases'
        'help:Show help'
    )

    if (( CURRENT == 2 )); then
        _describe 'command' commands
    elif (( CURRENT == 3 )); then
        case "$words[2]" in
            use|remove|rm|login|set-key|key)
                local -a profiles
                profiles=(${(f)"$(cw --completions 2>/dev/null)"})
                _describe 'profile' profiles
                ;;
        esac
    fi
}

_cw "$@"
